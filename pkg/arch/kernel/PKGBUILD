# Maintainer: Maximilian Luz <m@mxnluz.io>
# Maintainer: Bla≈æ Hrastnik <blaz@mxxn.io>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=linux-surface
pkgver=6.18.8.arch2
pkgrel=1
pkgdesc='Linux'
_shortver=${pkgver%.*}
_fullver=${pkgver%.*}-${pkgver##*.}
_srctag=v${_fullver}
url="https://github.com/archlinux/linux/commits/$_srctag"
arch=(x86_64)
license=(GPL2)
makedepends=(
  bc
  cpio
  gettext
  git
  libelf
  perl
  rust
  rust-bindgen
  rust-src
  tar
  xz
  python
  # we don't need `pahole` as deps because we disable `CONFIG_DEBUG_INFO`
  # ignoring deps for docs because we don't build docs package
)
optdepends=('iptsd: Touchscreen support'
            'linux-firmware: Firmware files for Linux'
            'linux-firmware-marvell: Firmware files for Marvell WiFi / Bluetooth')
options=(
  !debug
  !strip
)
_srcname=archlinux-linux
source=(
  "$_srcname::git+https://github.com/archlinux/linux#tag=$_srctag"
  config         # the main kernel config file

  surface.config # surface specific options
  arch.config # config changes for linux-surface kernel
  0001-secureboot.patch
  0002-surface3.patch
  0003-mwifiex.patch
  0004-ath10k.patch
  0005-ipts.patch
  0006-ithc.patch
  0007-surface-sam.patch
  0008-surface-sam-over-hid.patch
  0009-surface-button.patch
  0010-surface-typecover.patch
  0011-surface-shutdown.patch
  0012-surface-gpe.patch
  0013-cameras.patch
  0014-amd-gpio.patch
  0015-rtc.patch
  0016-hid-surface.patch
  0017-powercap.patch
)
validpgpkeys=(
  'ABAF11C65A2970B130ABE3C479BE3E4300411886'  # Linus Torvalds
  '647F28654894E3BD457199BE38DBBDC86092693E'  # Greg Kroah-Hartman
  'A2FF3A36AAA56654109064AB19802F8B0D70FC30'  # Jan Alexander Steffens (heftig)
)
sha256sums=('9526800a611ea4a46e8410686c89bab5a92df4b556b853e8933caa0d6cca835a'
            'd0ce1ee11ca0bc6a817c3c17a2651076409bd9fd6c0ab9e744aae2131ab654ce'
            '233a1c4d898b7720dd623421b694cc030075317b01cba6444b53faefc7b2ca55'
            '008e37de7e6cf8b9479ef0f0f425a25092646e85d741aedd056e5ff3e1096361'
            '25457105444c91e4de57c6938f4e36f98e2ce1d6f079b401ae8d6c2ada689c06'
            'a813b8fda4a4eadd13ad78fac982aba0abcad72398cd9989eaafcc8372d3d39b'
            'a421e02dcbad81ec20e7c0e9c83a26ea018b203d0da96090cce25f8da6c63982'
            'e3022dbf12cf2e24fe0be1777da129808d9827be89e09d34f4971c5bfda53972'
            '4b2972e73d311ac9f603e9229491464439a36537666f426797dcc70ecc611a4b'
            '7b908157f3c88a16106555513d36c00a20ceec78314600b9ad042c8e62a7a90a'
            '33347c5a7c7112ad08d83dbb54d0841c1044c62c9695a40a3cf09a54a65c4684'
            'c77726fee303a204f05385cacd6644a3e3452580056391d11873919b18ca5efb'
            'e40a928da09249e35f40072bee54f61a0b5d9aea12917674fa9c4a3944072e2b'
            'b6badd94dc5157c88ed64d34a38a4333d0cc6b19190d179a57a5a2fb3248bc24'
            '3722b78f24f19d653849864fc2ae24c782a2c83519620551191b96bcaac1a3cb'
            'aa9601d0e3e8c5268c3e975632e8de93e001515b25b8c0caf2c0428907783c68'
            'aaabc6fe397ad534e0d1654ffafefe8aebd9b8cbb52d05b0abb0c394e1dce650'
            'beff0cf807dfbcbe58505f9d101a910679e360a11bb82ce9194db367c844ea7c'
            '34ce59aa1f32cdcbb48307cbdc06de27376c909938fb6f8647c3dc47f859aac6'
            'bb2c5d4718eba576037d7d8c59dbd0227cadf5184b644071e26d441b38911e77'
            '4e5876d308e391fc2055a6b1671d8af0bb394e81e05bede7edcc829ab6ba3059')


export KBUILD_BUILD_HOST=archlinux
export KBUILD_BUILD_USER=$pkgbase
export KBUILD_BUILD_TIMESTAMP="$(date -Ru${SOURCE_DATE_EPOCH:+d @$SOURCE_DATE_EPOCH})"

# optional certificate and key for secure boot signing
_mok_crt="$PWD/MOK.crt"
_mok_key="$PWD/MOK.key"

_make() {
  test -s version
  make KERNELRELEASE="$(<version)" "$@"
}

prepare() {
  cd $_srcname

  echo "Setting version..."
  echo "-$pkgrel" > localversion.10-pkgrel
  echo "${pkgbase#linux}" > localversion.20-pkgname
  make defconfig
  make -s kernelrelease > version
  make mrproper

  # setup git
  git config --local user.email "surfacebot@users.noreply.github.com"
  git config --local user.name "surfacebot"

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    git am -3 "../$src"
  done

  echo "Setting config..."
  # cp ../config .config
  # merge the two configs together
  ./scripts/kconfig/merge_config.sh -m ../config ../surface.config ../arch.config
  _make olddefconfig

  echo "Prepared $pkgbase version $(<version)"
}

build() {
  cd $_srcname
  _make all
}

_package() {
  pkgdesc="The $pkgdesc kernel and modules"
  depends=(
    coreutils
    initramfs
    kmod
  )
  optdepends=(
    'linux-firmware: firmware images needed for some devices'
    'scx-scheds: to use sched-ext schedulers'
    'wireless-regdb: to set the correct wireless channels of your country'
  )
  provides=(
    linux=${_shortver}
    KSMBD-MODULE
    NTSYNC-MODULE
    VIRTUALBOX-GUEST-MODULES
    WIREGUARD-MODULE
  )
  replaces=(
    virtualbox-guest-modules-arch
    wireguard-arch
  )

  cd $_srcname
  local modulesdir="$pkgdir/usr/lib/modules/$(<version)"
  local image_name="$(_make -s image_name)"

  # sign boot image if the prequisites are available
  if [[ -f "$_mok_crt" ]] && [[ -f "$_mok_key" ]] && [[ -x "$(command -v sbsign)" ]]; then
    echo "Signing boot image..."
    sbsign --key "$_mok_key" --cert "$_mok_crt" --output "$image_name" "$image_name"
  fi

  echo "Installing boot image..."
  # systemd expects to find the kernel here to allow hibernation
  # https://github.com/systemd/systemd/commit/edda44605f06a41fb86b7ab8128dcf99161d2344
  install -Dm644 "$image_name" "$modulesdir/vmlinuz"

  # Used by mkinitcpio to name the kernel
  echo "$pkgbase" | install -Dm644 /dev/stdin "$modulesdir/pkgbase"

  echo "Installing modules..."
  ZSTD_CLEVEL=19 _make INSTALL_MOD_PATH="$pkgdir/usr" INSTALL_MOD_STRIP=1 \
    DEPMOD=/doesnt/exist modules_install  # Suppress depmod

  # remove build link
  rm "$modulesdir"/build
}

_package-headers() {
  pkgdesc="Headers and scripts for building modules for the $pkgdesc kernel"
  depends=(pahole)
  provides=("linux-headers=${_shortver}")

  cd $_srcname
  local builddir="$pkgdir/usr/lib/modules/$(<version)/build"

  echo "Installing build files..."
  install -Dt "$builddir" -m644 .config Makefile Module.symvers System.map \
    localversion.* version vmlinux
  install -Dt "$builddir/kernel" -m644 kernel/Makefile
  install -Dt "$builddir/arch/x86" -m644 arch/x86/Makefile
  cp -t "$builddir" -a scripts
  ln -srt "$builddir" "$builddir/scripts/gdb/vmlinux-gdb.py"

  # required when STACK_VALIDATION is enabled
  install -Dt "$builddir/tools/objtool" tools/objtool/objtool

  echo "Installing headers..."
  cp -t "$builddir" -a include
  cp -t "$builddir/arch/x86" -a arch/x86/include
  install -Dt "$builddir/arch/x86/kernel" -m644 arch/x86/kernel/asm-offsets.s

  install -Dt "$builddir/drivers/md" -m644 drivers/md/*.h
  install -Dt "$builddir/net/mac80211" -m644 net/mac80211/*.h

  # https://bugs.archlinux.org/task/13146
  install -Dt "$builddir/drivers/media/i2c" -m644 drivers/media/i2c/msp3400-driver.h

  # https://bugs.archlinux.org/task/20402
  install -Dt "$builddir/drivers/media/usb/dvb-usb" -m644 drivers/media/usb/dvb-usb/*.h
  install -Dt "$builddir/drivers/media/dvb-frontends" -m644 drivers/media/dvb-frontends/*.h
  install -Dt "$builddir/drivers/media/tuners" -m644 drivers/media/tuners/*.h

  # https://bugs.archlinux.org/task/71392
  install -Dt "$builddir/drivers/iio/common/hid-sensors" -m644 drivers/iio/common/hid-sensors/*.h

  echo "Installing KConfig files..."
  find . -name 'Kconfig*' -exec install -Dm644 {} "$builddir/{}" \;

  echo "Installing Rust files..."
  install -Dt "$builddir/rust" -m644 rust/*.rmeta
  install -Dt "$builddir/rust" rust/*.so

  echo "Installing unstripped VDSO..."
  make INSTALL_MOD_PATH="$pkgdir/usr" vdso_install \
    link=  # Suppress build-id symlinks

  echo "Removing unneeded architectures..."
  local arch
  for arch in "$builddir"/arch/*/; do
    [[ $arch = */x86/ ]] && continue
    echo "Removing $(basename "$arch")"
    rm -r "$arch"
  done

  echo "Removing documentation..."
  rm -r "$builddir/Documentation"

  echo "Removing broken symlinks..."
  find -L "$builddir" -type l -printf 'Removing %P\n' -delete

  echo "Removing loose objects..."
  find "$builddir" -type f -name '*.o' -printf 'Removing %P\n' -delete

  echo "Stripping build tools..."
  local file
  while read -rd '' file; do
    case "$(file -Sib "$file")" in
      application/x-sharedlib\;*)      # Libraries (.so)
        strip -v $STRIP_SHARED "$file" ;;
      application/x-archive\;*)        # Libraries (.a)
        strip -v $STRIP_STATIC "$file" ;;
      application/x-executable\;*)     # Binaries
        strip -v $STRIP_BINARIES "$file" ;;
      application/x-pie-executable\;*) # Relocatable binaries
        strip -v $STRIP_SHARED "$file" ;;
    esac
  done < <(find "$builddir" -type f -perm -u+x ! -name vmlinux -print0)

  echo "Stripping vmlinux..."
  strip -v $STRIP_STATIC "$builddir/vmlinux"

  echo "Adding symlink..."
  mkdir -p "$pkgdir/usr/src"
  ln -sr "$builddir" "$pkgdir/usr/src/$pkgbase"
}

pkgname=("$pkgbase" "$pkgbase-headers")
for _p in "${pkgname[@]}"; do
  eval "package_$_p() {
    $(declare -f "_package${_p#$pkgbase}")
    _package${_p#$pkgbase}
  }"
done

# vim:set ts=8 sts=2 sw=2 et:
